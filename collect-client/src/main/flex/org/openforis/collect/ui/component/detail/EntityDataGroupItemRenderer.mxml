<?xml version="1.0" encoding="utf-8"?>
<datagroup:MultipleNodesDataGroupItemRenderer 
				xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:detail="org.openforis.collect.ui.component.detail.*" 
				xmlns:datagroup="org.openforis.collect.ui.component.datagroup.*"
				>
	
	<fx:Script>
		<![CDATA[
			import mx.binding.utils.BindingUtils;
			import mx.collections.IList;
			
			import org.openforis.collect.Application;
			import org.openforis.collect.metamodel.proxy.AttributeDefinitionProxy;
			import org.openforis.collect.metamodel.proxy.EntityDefinitionProxy;
			import org.openforis.collect.metamodel.proxy.ModelVersionProxy;
			import org.openforis.collect.metamodel.proxy.NodeDefinitionProxy;
			import org.openforis.collect.metamodel.ui.UIOptions$Direction;
			import org.openforis.collect.model.proxy.EntityProxy;
			import org.openforis.collect.presenter.EntityDataGroupItemPresenter;
			import org.openforis.collect.ui.UIBuilder;

			public static const DIRECTION_HORIZONTAL:String = "horizontal";
			public static const DIRECTION_VERTICAL:String = "vertical";
			public static const DEFAULT_LABEL_WIDTH:Number = 150;
			
			private var _entityDefinition:EntityDefinitionProxy;
			private var _nodeDefinitions:IList;
			private var _modelVersion:ModelVersionProxy;
			private var _entity:EntityProxy;
			private var _nodesDirection:String = DIRECTION_HORIZONTAL;
			
			override protected function initPresenter():void {
				_presenter = new EntityDataGroupItemPresenter(this);	
				_presenter.init();
			}

			override protected function createChildren():void {
				super.createChildren();
				contentGroup.addElement(rowNumberLabel);
				contentGroup.addElement(nodeDefinitionsGroup);
			}
			
			override public function set data(value:Object):void {
				super.data = value;
				entity = value as EntityProxy;
				modelVersion = Application.activeRecord.version;
				if ( entity != null && nodeDefinitionsGroup.numElements == 0 ) {
					initNodeDefinitionItemRenderers();
				}
			}
			
			protected function initNodeDefinitionItemRenderers():void {
				modelVersion = Application.activeRecord.version;
				entityDefinition = _entity.definition as EntityDefinitionProxy;
				nodeDefinitions = _entityDefinition.getDefinitionsInVersion(_modelVersion);
				nodesDirection = _entityDefinition.direction == UIOptions$Direction.BY_COLUMNS ?
					DIRECTION_VERTICAL: DIRECTION_HORIZONTAL;
				dragEnabled = Application.activeRecordEditable && !(_entityDefinition.enumerable && _entityDefinition.enumerate);
				addNodeDefinitionItemRenderers();
			}
			
			protected function addNodeDefinitionItemRenderers():void {
				nodeDefinitionsGroup.removeAllElements();

				for each (var nodeDefn:NodeDefinitionProxy in nodeDefinitions) {
					var formItem:CollectFormItem;
					if(nodeDefn is AttributeDefinitionProxy) {
						formItem = UIBuilder.getAttributeFormItem(AttributeDefinitionProxy(nodeDefn)) as CollectFormItem;
						formItem.labelWidth = DEFAULT_LABEL_WIDTH;
					} else {
						formItem = UIBuilder.getEntityFormItem(EntityDefinitionProxy(nodeDefn)) as CollectFormItem;
						BindingUtils.bindProperty(formItem, "modelVersion", this, "modelVersion");
					}
					BindingUtils.bindProperty(formItem, "parentEntity", this, "entity");
					nodeDefinitionsGroup.addElement(formItem);
				}
			}
			
			[Bindable]
			public function get entityDefinition():EntityDefinitionProxy {
				return _entityDefinition;
			}
			
			public function set entityDefinition(value:EntityDefinitionProxy):void {
				_entityDefinition = value;
			}
			
			[Bindable]
			public function get modelVersion():ModelVersionProxy {
				return _modelVersion;
			}
			
			public function set modelVersion(value:ModelVersionProxy):void {
				_modelVersion = value;
			}
			
			[Bindable]
			public function get nodeDefinitions():IList {
				return _nodeDefinitions;
			}
			
			public function set nodeDefinitions(value:IList):void {
				_nodeDefinitions = value;
			}
			
			[Bindable]
			public function get entity():EntityProxy {
				return _entity;
			}
			
			public function set entity(value:EntityProxy):void {
				_entity = value;
			}
			
			[Bindable]
			public function get nodesDirection():String {
				return _nodesDirection;
			}
			
			public function set nodesDirection(value:String):void {
				_nodesDirection = value;
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		
		<s:HorizontalLayout id="horizontalLayout" gap="4" />
		<s:VerticalLayout id="verticalLayout" gap="2" />
		
		<s:Label id="rowNumberLabel"
				 styleName="formLabel"
				 visible="{entityDefinition.showRowNumbers}"
				 includeInLayout="{entityDefinition.showRowNumbers}"
				 text="{entity.index + 1}."
				 width="20"
				 height="22"
				 verticalAlign="middle"
				 textAlign="right"
				 />
		
		<s:Group id="nodeDefinitionsGroup"
				 layout="{nodesDirection == DIRECTION_HORIZONTAL ? horizontalLayout: verticalLayout}" />

	</fx:Declarations>
	
</datagroup:MultipleNodesDataGroupItemRenderer>
